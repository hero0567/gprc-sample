buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

plugins {
    id "com.google.protobuf" version "0.8.6"
    id "java"
    id "application"
    id "idea"
    id 'com.github.ben-manes.versions' version '0.19.0'
}

version '1.0-SNAPSHOT'

def protoFilesDir = "$projectDir/../../proto-files"
def protoSrcPath = "$projectDir/src/generatedProtoSrc"

sourceSets {
    main {
        proto {
            srcDir protoFilesDir
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.6.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = protoSrcPath
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'io.grpc:grpc-all:1.12.0'
}

idea {
    module {
        sourceDirs += file("${protoSrcPath}/main/java")
        sourceDirs += file("${protoSrcPath}/main/grpc")
    }
}

task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'helloworld.HelloWorldServer'
}


startScripts.enabled = false

task helloWorldServer(type: CreateStartScripts) {
    mainClassName = "HelloWorldServer"
    applicationName = "HelloWorld"
    outputDir = new File(project.buildDir, mainClassName)
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(helloWorldServer)
    fileMode = 0755
}
